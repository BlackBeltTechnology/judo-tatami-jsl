
///////////////////////////////////////////////////////////////
// Transfer composite relation
///////////////////////////////////////////////////////////////

@abstract
rule AbstractCreateTransferObjectEmbeddedRelationForTransferFieldDeclaration
    transform s: JSL!TransferFieldDeclaration
    to t: JUDOPSM!TransferObjectRelation {
        guard: s.referenceType.isKindOf(JSL!TransferDeclaration)

        t.name = s.name;
        t.target = s.referenceType.getPSMEquivalent();

        // TODO: 
        // t.embeddedCreate = not s.eContainer.isAbstract;
        // t.embeddedUpdate = not s.eContainer.isAbstract;
        // t.embeddedDelete = not s.eContainer.isAbstract;
        
		t.embedded = true;
}


@greedy
rule CreateTransientTransferObjectEmbeddedRelationForTransferFieldDeclaration
    transform s: JSL!TransferFieldDeclaration
    to t: JUDOPSM!TransferObjectRelation 
    extends AbstractCreateTransferObjectEmbeddedRelationForTransferFieldDeclaration {
        guard: s.referenceType.isKindOf(JSL!TransferDeclaration) and not s.maps.isDefined() and not s.reads.isDefined() and not s.eContainer.automap

        t.setId("(jsl/" + s.getId() + ")/CreateTransientTransferObjectEmbeddedRelationForTransferFieldDeclaration");
        t.cardinality = s.equivalentWithPrefix("CreateCardinalityForTransferFieldDeclaration", t.getId());

        var def = JSL!TransferDefault.all().selectOne(d | d.field.reference == s);
        if (def.isDefined()) {
           if (s.eContainer.map.isUndefined()) {
              t.defaultValue = def.equivalent("CreateDefaultStaticNavigationForUnmappedTransferObjectConstructor");
           } else {
              t.defaultValue = def.equivalent("CreateDefaultNavigationPropertyForMappedTransferObjectConstructor");
           }
        }      

        s.eContainer.getPSMEquivalent().relations.add(t);

        log.debug("Created TransferObjectRelation for TransferFieldDeclaration: [" + t.name + "] into [" + t.eContainer.name + "]");
}

@greedy
rule CreateDerivedTransferObjectEmbeddedRelationForTransferFieldDeclaration
    transform s : JSL!TransferFieldDeclaration
    to t : JUDOPSM!TransferObjectRelation
    extends AbstractCreateTransferObjectEmbeddedRelationForTransferFieldDeclaration {
      guard: s.referenceType.isKindOf(JSL!TransferDeclaration) and s.reads.isDefined() and not s.eContainer.automap

      t.setId("(jsl/" + s.getId() + ")/CreateDerivedTransferObjectEmbeddedRelationForTransferFieldDeclaration");
      t.cardinality = s.equivalentWithPrefix("CreateCardinalityForTransferFieldDeclaration", t.getId());
      
      if (s.eContainer.map.isDefined()) {
         t.binding = s.reads.equivalent("CreateReadsNavigationPropertyForMappedTransferObjectTransferRelationDeclaration");
      } else {
         t.binding = s.reads.equivalent("CreateReadsReferenceExpressionForUnmappedTransferObjectTransferRelationDeclaration");
      }

      s.eContainer.getPSMEquivalent().relations.add(t);
      log.debug("Created TransferObjectRelation for TransferFieldDeclaration: [" + t.name + "] into [" + t.eContainer.name + "]");
}

@greedy
rule CreateMappedTransferObjectEmbeddedRelationForTransferFieldDeclaration
    transform s : JSL!TransferFieldDeclaration
    to t : JUDOPSM!TransferObjectRelation
    extends AbstractCreateTransferObjectEmbeddedRelationForTransferFieldDeclaration {
      guard: s.referenceType.isKindOf(JSL!TransferDeclaration) and s.maps.isDefined() and not s.eContainer.automap

      t.setId("(jsl/" + s.getId() + ")/CreateMappedTransferObjectEmbeddedRelationForTransferFieldDeclaration");
      t.cardinality = s.equivalentWithPrefix("CreateCardinalityForTransferFieldDeclaration", t.getId());
      
      var entityMember = s.maps.features.first.member;
      
      if (entityMember.isKindOf(JSL!EntityFieldDeclaration)) {
	      t.binding = entityMember.equivalent("CreateContainmentFromField");
      } else if (entityMember.isKindOf(JSL!EntityRelationDeclaration)) {
	      t.binding = entityMember.equivalent("CreateDeclaredAssociationEnd");      
      } else if (entityMember.isKindOf(JSL!EntityRelationOppositeInjected)) {
	      t.binding = entityMember.equivalent("CreateNamedOppositeAssociationEnd");      
      } else {
          throw "Invalid member type - " + s.eContainer.name + "." + e.name + " " + entityMember;
      }

      var def = JSL!TransferDefault.all().selectOne(d | d.field.reference == s);
      if (def.isDefined()) {
        t.defaultValue = def.equivalent("CreateDefaultNavigationPropertyForMappedTransferObjectConstructor");
      }      
      
      s.eContainer.getPSMEquivalent().relations.add(t);
      log.debug("Created TransferObjectRelation for TransferFieldDeclaration: [" + t.name + "] into [" + t.eContainer.name + "]");
}

////////////////////////////////////////////////////////////////////////////////////////////
// Relation default value
////////////////////////////////////////////////////////////////////////////////////////////

@greedy
rule CreateTransferEntityDefaultValueRelationForMappedTransferObjectConstructor
    transform s: JSL!TransferDefault
    to t: JUDOPSM!TransferObjectRelation {
        guard: s.field.reference.referenceType.isKindOf(JSL!TransferDeclaration) and s.field.reference.eContainer.map.isDefined()
        t.setId("(jsl/" + s.field.reference.getId() + ")/CreateTransferEntityDefaultValueRelationForMappedTransferObjectConstructor");
        t.name = defaultDefaultNamePrefix + s.field.reference.name + defaultDefaultNameMidfix + s.field.reference.eContainer.name + defaultDefaultNamePostfix;
        t.binding = s.equivalent("CreateDefaultNavigationPropertyForMappedTransferObjectConstructor");
        t.target = s.field.reference.referenceType.getPSMEquivalent();        
        t.cardinality = s.field.reference.equivalentWithPrefix("CreateCardinalityForTransferFieldDeclaration", t.getId());
        s.eContainer.eContainer.getPSMEquivalent().relations.add(t);
}


@greedy
rule CreateTransferEntityDefaultValueRelationForUnmappedTransferObjectConstructor
    transform s: JSL!TransferDefault
    to t: JUDOPSM!TransferObjectRelation {
        guard: s.field.reference.referenceType.isKindOf(JSL!TransferDeclaration) and s.field.reference.eContainer.map.isUndefined()
        t.setId("(jsl/" + s.field.reference.getId() + ")/CreateTransferEntityDefaultValueRelationForUnmappedTransferObjectConstructor");
        t.name = defaultDefaultNamePrefix + s.field.reference.name + defaultDefaultNameMidfix + s.field.reference.eContainer.name + defaultDefaultNamePostfix;
        t.binding = s.equivalent("CreateDefaultStaticNavigationForUnmappedTransferObjectConstructor");
        t.target = s.field.reference.referenceType.getPSMEquivalent();        
        t.cardinality = s.field.reference.equivalentWithPrefix("CreateCardinalityForTransferFieldDeclaration", t.getId());
        s.eContainer.eContainer.getPSMEquivalent().relations.add(t);
}


