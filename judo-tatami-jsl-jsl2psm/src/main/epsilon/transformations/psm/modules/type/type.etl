import "../namespace/namespace.etl";
import "../../../../operations/_importAll.eol";

rule CreateNumericType
	transform s: JSL!DataTypeDeclaration
	to t: JUDOPSM!NumericType {
        guard: s.`primitive` == "numeric"

        t.setId("(jsl/" + s.getId() + ")/CreateNumericType");
        t.name = s.name;
        t.precision = s.precision.precision.intValue();
        t.scale = s.scale.scale.intValue();
        s.eContainer.equivalent("CreateGeneratedTypesPackage").elements.add(t);
        log.debug("Created NumericType: " + t.name);
    }

rule CreateDateType
  transform s: JSL!DataTypeDeclaration
  to t: JUDOPSM!DateType {
        guard: s.`primitive` == "date"

        t.setId("(jsl/" + s.getId() + ")/CreateDateType");
        t.name = s.name;
        s.eContainer.equivalent("CreateGeneratedTypesPackage").elements.add(t);
        log.debug("Created DateType: " + t.name);
  }

rule CreateTimeType
  transform s: JSL!DataTypeDeclaration
  to t: JUDOPSM!TimeType {
        guard: s.`primitive` == "time"

        t.setId("(jsl/" + s.getId() + ")/CreateTimeType");
        t.name = s.name;
        s.eContainer.equivalent("CreateGeneratedTypesPackage").elements.add(t);
        log.debug("Created TimeType: " + t.name);
  }

rule CreateTimestampType
	transform s: JSL!DataTypeDeclaration
	to t: JUDOPSM!TimestampType {
        guard: s.`primitive` == "timestamp"

        t.setId("(jsl/" + s.getId() + ")/CreateTimestampType");
        t.name = s.name;
        s.eContainer.equivalent("CreateGeneratedTypesPackage").elements.add(t);
        log.debug("Created TimestampType: " + t.name);
    }

rule CreateEnumerationType
	transform s: JSL!EnumDeclaration
	to t: JUDOPSM!EnumerationType {
        t.setId("(jsl/" + s.getId() + ")/CreateEnumerationType");
        t.name = s.name;
        s.eContainer.equivalent("CreateGeneratedTypesPackage").elements.add(t);
        log.debug("Created EnumerationType: " + t.name);
    }

rule CreateEnumerationMember
	transform s: JSL!EnumLiteral
	to t: JUDOPSM!EnumerationMember {
        t.setId("(jsl/" + s.getId() + ")/CreateEnumerationMember");
        t.name = s.name;
        t.ordinal = s.value.intValue();
        s.eContainer.equivalent("CreateEnumerationType").members.add(t);
        log.debug("Created EnumerationMember: " + t.name);
    }
    
rule CreateBooleanType 
	transform s : JSL!DataTypeDeclaration
	to t : JUDOPSM!BooleanType {
        guard: s.`primitive` == "boolean"
        
        t.setId("(jsl/" + s.getId() + ")/CreateBooleanType");
        t.name = s.name;
        s.eContainer.equivalent("CreateGeneratedTypesPackage").elements.add(t);
        log.debug("Created BooleanType: " + t.name);
}
