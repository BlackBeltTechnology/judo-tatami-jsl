import "../../../../operations/_importAll.eol";

rule CreateContainmentFromField
    transform s: JSL!EntityFieldDeclaration
    to t: JUDOPSM!Containment
    {
        guard: s.getReferenceType().isKindOf(JSL!EntityDeclaration) and not s.isCalculated()

        t.setId("(jsl/" + s.getId() + ")/CreateContainmentFromField");
        t.name = s.name;
        t.target = s.getReferenceType().getEntityDeclarationEquivalent();
        t.cardinality = s.equivalentDiscriminated("CreateCardinalityForFieldDeclaration", t.getId());

        s.eContainer.getEntityDeclarationEquivalent().relations.add(t);
        log.debug("Created Containment: " + t.name);
    }

//////////////////////////////////////////////////////////////////
// Entity TransferField Reads
//////////////////////////////////////////////////////////////////
@greedy
rule CreateReadsReferenceExpressionTypeForTransferRelationDeclaration
    transform s : JSL!Expression
    to t: JUDOPSM!ReferenceExpressionType {
        guard: s.isExpressionForTransferRelationReads() and s.eContainer.getReferenceType().isKindOf(JSL!TransferDeclaration)

        t.setId("(jsl/" + s.getId() + ")/CreateReadsReferenceExpressionTypeForTransferRelationDeclaration");
        t.expression = expressionUtils.getJqlForExpression(s, entityNamePrefix, entityNamePostfix);

        if (s.eContainer.eContainer.map.isDefined()) {
            s.equivalent("CreateReadsNavigationPropertyForMappedTransferObjectTransferRelationDeclaration").getterExpression = t;
        } else {
            s.equivalent("CreateReadsStaticNavigationForUnmappedTransferObjectTransferRelationDeclaration").getterExpression = t;
        }

        log.debug("Created ReferenceExpressionType for TransferRelation Reads: " + t.expression);
    }

@greedy
rule CreateReadsStaticNavigationForUnmappedTransferObjectTransferRelationDeclaration
    transform s : JSL!Expression
    to t: JUDOPSM!StaticNavigation {
        guard: s.isExpressionForTransferRelationReads() and s.eContainer.getReferenceType().isKindOf(JSL!TransferDeclaration)
           and s.eContainer.eContainer.map.isUndefined()

        t.setId("(jsl/" + s.getId() + ")/CreateReadsStaticNavigationForUnmappedTransferObjectTransferRelationDeclaration");
        t.name = defaultReadsNamePrefix + s.eContainer.name + defaultReadsNameMidfix + s.eContainer.eContainer.name + defaultReadsNamePostfix;
        t.target = s.eContainer.getReferenceType().getTransferDeclarationEquivalent().entityType;
        t.cardinality = s.eContainer.equivalentDiscriminated("CreateCardinalityForTransferRelationDeclaration", t.getId());

        s.eContainer.eContainer.eContainer.getModelRoot().elements.add(t);

        log.debug("Created StaticNavigation for TransferRelation Reads: " + s);
    }

@greedy
rule CreateReadsNavigationPropertyForMappedTransferObjectTransferRelationDeclaration
    transform s : JSL!Expression
    to t: JUDOPSM!NavigationProperty {
        guard: s.isExpressionForTransferRelationReads() and s.eContainer.getReferenceType().isKindOf(JSL!TransferDeclaration)
           and s.eContainer.eContainer.map.isDefined()

        t.setId("(jsl/" + s.getId() + ")/CreateReadsNavigationPropertyForMappedTransferObjectTransferRelationDeclaration");
        t.name = defaultReadsNamePrefix + s.eContainer.name + defaultReadsNameMidfix + s.eContainer.eContainer.name + defaultReadsNamePostfix;
        t.target = s.eContainer.getReferenceType().getTransferDeclarationEquivalent().entityType;
        t.cardinality = s.eContainer.equivalentDiscriminated("CreateCardinalityForTransferRelationDeclaration", t.getId());

        s.eContainer.eContainer.map.entity.getEntityDeclarationEquivalent().navigationProperties.add(t);

        log.debug("Created NavigationProperty for TransferRelation Reads: " + s);
    }

    
//////////////////////////////////////////////////////////////////
// Transfer Object Constructors Field Default Expression
//////////////////////////////////////////////////////////////////   

@greedy
rule CreateDefaultReferenceExpressionTypeForMappedTransferObjectConstructor
    transform s : JSL!DefaultModifier
    to t: JUDOPSM!ReferenceExpressionType {
        guard: s.eContainer.isKindOf(JSL!TransferRelationDeclaration) and s.eContainer.eContainer.map.isDefined()

        t.setId("(jsl/" + s.eContainer.getId() + ")/CreateDefaultReferenceExpressionTypeForMappedTransferObjectConstructor");
        t.expression = expressionUtils.getJqlForExpression(s.expression, entityNamePrefix, entityNamePostfix);
        s.equivalent("CreateDefaultNavigationPropertyForMappedTransferObjectConstructor").getterExpression = t;

        log.debug("Created ReferenceExpressionType for Transfer Object Default Value: " + t.expression);
    }

@greedy
rule CreateDefaultNavigationPropertyForMappedTransferObjectConstructor
    transform s : JSL!DefaultModifier
    to t: JUDOPSM!NavigationProperty {
        guard: s.eContainer.isKindOf(JSL!TransferRelationDeclaration) and s.eContainer.eContainer.map.isDefined()

        t.setId("(jsl/" + s.eContainer.getId() + ")/CreateDefaultNavigationPropertyForMappedTransferObjectConstructor");
        t.name = defaultDefaultNamePrefix + s.eContainer.name + defaultDefaultNameMidfix + s.eContainer.eContainer.name + defaultDefaultNamePostfix;
        t.target = s.eContainer.getReferenceType().getTransferDeclarationEquivalent().entityType;
  	    t.cardinality = s.eContainer.equivalentDiscriminated("CreateCardinalityForTransferRelationDeclaration", t.getId());
        s.eContainer.eContainer.map.entity.getEntityDeclarationEquivalent().navigationProperties.add(t);

        log.debug("Created NavigationProperty for Transfer Object Default Value: " + s);
    }

@greedy
rule CreateDefaultReferenceExpressionTypeForUnmappedTransferObjectConstructor
    transform s : JSL!DefaultModifier
    to t: JUDOPSM!ReferenceExpressionType {
        guard: s.eContainer.isKindOf(JSL!TransferRelationDeclaration) and s.eContainer.eContainer.map.isUndefined()
        
        t.setId("(jsl/" + s.eContainer.getId() + ")/CreateDefaultReferenceExpressionTypeForUnmappedTransferObjectConstructor");
        t.expression = expressionUtils.getJqlForExpression(s.expression, entityNamePrefix, entityNamePostfix);
        s.equivalent("CreateDefaultStaticNavigationForUnmappedTransferObjectConstructor").getterExpression = t;

        log.debug("Created ReferenceExpressionType for Transfer Object Default Value: " + t.expression);
    }

@greedy
rule CreateDefaultStaticNavigationForUnmappedTransferObjectConstructor
    transform s : JSL!DefaultModifier
    to t: JUDOPSM!StaticNavigation {
        guard: s.eContainer.isKindOf(JSL!TransferRelationDeclaration) and s.eContainer.eContainer.map.isUndefined()

        t.setId("(jsl/" + s.eContainer.getId() + ")/CreateDefaultStaticNavigationForUnmappedTransferObjectConstructor");
        t.name = defaultDefaultNamePrefix + s.eContainer.name + defaultDefaultNameMidfix + s.eContainer.eContainer.name + defaultDefaultNamePostfix;
        t.target = s.eContainer.getReferenceType().getTransferDeclarationEquivalent().entityType;
  	    t.cardinality = s.eContainer.equivalentDiscriminated("CreateCardinalityForTransferRelationDeclaration", t.getId());

        s.eContainer.eContainer.eContainer.getModelRoot().elements.add(t);
        
        log.debug("Created StaticNavigation for Transfer Object Default Value: " + s);
    }
