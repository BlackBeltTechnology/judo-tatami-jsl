
rule CreateUnmappedTransferObjectForQuery
    transform s : JSL!QueryDeclaration
    to t : JUDOPSM!UnmappedTransferObjectType {
		guard: s.parameters.size() > 0
		
        t.setId("(jsl/" + s.getId() + ")/CreateUnmappedTransferObjectForQuery");
        t.name = defaultParameterNamePrefix + s.eContainer.name + defaultParameterNameMidfix + s.name + defaultParameterNamePostfix;
		s.eContainer.getModelRoot().elements.add(t);
		log.debug("Created UnmappedTransferObjectType: " + t.name);
}

rule CreateTransferObjectForQueryDeclarationParameter
    transform s : JSL!QueryDeclarationParameter
    to t: JUDOPSM!TransferAttribute {
        guard: s.eContainer.isKindOf(JSL!QueryDeclaration)
        
        t.setId("(jsl/" + s.getId() + ")/CreateTransferObjectForQueryDeclarationParameter");

        t.name = s.name;
        t.dataType = s.referenceType.getPSMEquivalent();
        
		s.eContainer.equivalent("CreateUnmappedTransferObjectForQuery").attributes.add(t);
        log.debug("Created Transfer Attribute for Query Declaration: " + s.name);
}

rule CreateStaticDataForQuery
    transform s : JSL!QueryDeclaration
    to t : JUDOPSM!StaticData
    {
      guard: s.referenceType.isKindOf(JSL!PrimitiveDeclaration)
      t.name = s.name;
      t.setId("(jsl/" + s.getId() + ")/CreateDataPropertyForQuery");
      t.getterExpression = s.equivalent("CreateGetterExpressionForParametrizedDataType");
      t.dataType = s.referenceType.getPSMEquivalent();

      s.eContainer.getModelRoot().elements.add(t);
      log.debug("Created Query Static Data: " + t.name);
}

rule CreateStaticNavigationForQuery
    transform s : JSL!QueryDeclaration
    to t : JUDOPSM!StaticNavigation
    {
      guard: s.referenceType.isKindOf(JSL!EntityDeclaration)
      t.name = s.name;
      t.setId("(jsl/" + s.getId() + ")/NavigationProperty");
      t.getterExpression = s.equivalent("CreateGetterExpressionForQueryParametrizedReferenceType");
	  t.cardinality = s.equivalent("CreateCardinalityForQueryDeclaration");
      t.target = s.referenceType.getPSMEquivalent();

      s.eContainer.getModelRoot().elements.add(t);
      log.debug("Created Query Static Navigation: " + t.name);
}

@lazy
rule CreateGetterExpressionForQueryParametrizedDataType
    transform s : JSL!QueryDeclaration
    to t: JUDOPSM!DataExpressionType {
        t.setId("(jsl/" + s.getId() + ")/CreateGetterExpressionForQueryParametrizedDataType");
        t.expression = expressionUtils.getJqlForEntityQuery(s, entityNamePrefix, entityNamePostfix);

		if (s.parameters.size() > 0) {
			t.parameterType = s.equivalent("CreateUnmappedTransferObjectForQuery");
		}

        log.debug("Created Data Expression Type for Parametrized Data Property: " + s.name);
}

@lazy
rule CreateGetterExpressionForQueryParametrizedReferenceType
    transform s : JSL!QueryDeclaration
    to t: JUDOPSM!ReferenceExpressionType {
        t.setId("(jsl/" + s.getId() + ")/CreateGetterExpressionForQueryParametrizedReferenceType");
        t.expression = expressionUtils.getJqlForStaticQuery(s, entityNamePrefix, entityNamePostfix);

		if (s.parameters.size() > 0) {
			t.parameterType = s.equivalent("CreateUnmappedTransferObjectForQuery");
		}

        log.debug("Created Reference Expression Type for Parametrized Reference Type: " + s.name);
}

