import "../../../../operations/_importAll.eol";
import "../namespace/namespace.etl";

@abstract
rule CreatePrimitiveTypedElement
    transform s: JSL!EntityMemberDeclaration
    to t: JUDOPSM!PrimitiveTypedElement {
        t.required = s.isRequired;
        t.name = s.name;
    }

rule CreateAttributeFromField
    transform s: JSL!EntityFieldDeclaration
    to t: JUDOPSM!Attribute
    extends CreatePrimitiveTypedElement {
        guard: s.referenceType.isKindOf(JSL!PrimitiveDeclaration)
        t.setId("(jsl/" + s.getId() + ")/CreateAttributeFromField");
        t.dataType = s.referenceType.getPSMEquivalent();
        s.eContainer.getPSMEquivalent().attributes.add(t);
        log.debug("Created Attribute (Field): " + t.name);
    }

rule CreateAttributeFromIdentifier
    transform s: JSL!EntityIdentifierDeclaration
    to t: JUDOPSM!Attribute
    extends CreatePrimitiveTypedElement {
        t.setId("(jsl/" + s.getId() + ")/CreateAttributeFromIdentifier");

        t.dataType = s.referenceType.getPSMEquivalent();
        t.identifier = true;
        s.eContainer.getPSMEquivalent().attributes.add(t);
        log.debug("Created Attribute (Identifier): " + t.name);
    }
    

//////////////////////////////////////////////////////////////////
// Primitive Field Default Expression
//////////////////////////////////////////////////////////////////   
@greedy
rule CreateDefaultDataExpressionTypeForPrimitiveEntityMember
    transform s : JSL!Expression
    to t: JUDOPSM!DataExpressionType {
        guard: s.isDefaultValueForEntity() and s.eContainer.referenceType.isKindOf(JSL!PrimitiveDeclaration)

        t.setId("(jsl/" + s.getId() + ")/CreateDefaultDataExpressionTypeForPrimitiveEntityMember");
        t.expression = expressionUtils.getJqlForExpression(s, entityNamePrefix, entityNamePostfix);
        s.equivalent("CreateDefaultValueForPrimitiveEntityMember").getterExpression = t;

        log.debug("Created DataExpressionType for Default Value: " + t.expression);
    }

@greedy
rule CreateDefaultValueForPrimitiveEntityMember
    transform s : JSL!Expression
    to t: JUDOPSM!DataProperty {
        guard: s.isDefaultValueForEntity() and s.eContainer.referenceType.isKindOf(JSL!PrimitiveDeclaration)

        t.setId("(jsl/" + s.getId() + ")/CreateDefaultValueForPrimitiveEntityMember");
        t.name = defaultDefaultNamePrefix + s.eContainer.name + defaultDefaultNameMidfix + s.eContainer.eContainer.name + defaultDefaultNamePostfix;
        t.dataType = s.eContainer.referenceType.getPSMEquivalent();
        t.required = false;

        s.eContainer.eContainer.getPSMEquivalent().dataProperties.add(t);

        log.debug("Created DataProperty for Default Value: " + s);
    }

//////////////////////////////////////////////////////////////////
// Error Field Default Expression
//////////////////////////////////////////////////////////////////   
@greedy
rule CreateDefaultDataExpressionTypeForPrimitiveErrorField
    transform s : JSL!Expression
    to t: JUDOPSM!DataExpressionType {
        guard: s.isDefaultValueForError() and s.eContainer.referenceType.isKindOf(JSL!PrimitiveDeclaration)

        t.setId("(jsl/" + s.getId() + ")/CreateDefaultDataExpressionTypeForPrimitiveErrorField");
        t.expression = expressionUtils.getJqlForExpression(s, entityNamePrefix, entityNamePostfix);
        s.equivalent("CreateDefaultValueForPrimitiveErrorField").getterExpression = t;

        log.debug("Created DefaultExpressionType for Default Value: " + t.expression);
    }

@greedy
rule CreateDefaultValueForPrimitiveErrorField
    transform s : JSL!Expression
    to t: JUDOPSM!StaticData {
        guard: s.isDefaultValueForError() and s.eContainer.referenceType.isKindOf(JSL!PrimitiveDeclaration)

        t.setId("(jsl/" + s.getId() + ")/CreateDefaultValueForPrimitiveErrorField");
        t.name = defaultDefaultNamePrefix + s.eContainer.name + defaultDefaultNameMidfix + s.eContainer.eContainer.name + defaultDefaultNamePostfix;
        t.dataType = s.eContainer.referenceType.getPSMEquivalent();
        t.required = false;

        s.eContainer.eContainer.eContainer.getModelRoot().elements.add(t);

        log.debug("Created StaticData for Default Value: " + s);
    }


//////////////////////////////////////////////////////////////////
// Primitive TransferField Reads
//////////////////////////////////////////////////////////////////   
@greedy
rule CreateReadsDataExpressionTypeForTransferFieldDeclaration
    transform s : JSL!Expression
    to t: JUDOPSM!DataExpressionType {
        guard: s.isExpressionForTransferFieldReads() and s.eContainer.referenceType.isKindOf(JSL!PrimitiveDeclaration)

        t.setId("(jsl/" + s.getId() + ")/CreateReadsDataExpressionTypeForTransferFieldDeclaration");
        t.expression = expressionUtils.getJqlForExpression(s, entityNamePrefix, entityNamePostfix);
        
        if (s.eContainer.eContainer.map.isDefined()) {    
	        s.equivalent("CreateReadsDataPropertyForMappedTransferObjectTranferFieldDeclaration").getterExpression = t;
        } else {
	        s.equivalent("CreateReadsStaticDataForUnmappedTransferObjectTranferFieldDeclaration").getterExpression = t;
        }

        log.debug("Created DataExpressionType for TransferField Reads: " + t.expression);
    }

@greedy
rule CreateReadsStaticDataForUnmappedTransferObjectTranferFieldDeclaration
    transform s : JSL!Expression
    to t: JUDOPSM!StaticData {
        guard: s.isExpressionForTransferFieldReads() and s.eContainer.referenceType.isKindOf(JSL!PrimitiveDeclaration)
           and s.eContainer.eContainer.map.isUndefined()

        t.setId("(jsl/" + s.getId() + ")/CreateReadsStaticDataForUnmappedTransferObjectTransferFieldDeclaration");
        t.name = defaultReadsNamePrefix + s.eContainer.name + defaultReadsNameMidfix + s.eContainer.eContainer.name + defaultReadsNamePostfix;
        t.dataType = s.eContainer.referenceType.getPSMEquivalent();
        t.required = false;

        s.eContainer.eContainer.eContainer.getModelRoot().elements.add(t);

        log.debug("Created StaticData for TransferField Reads: " + s);
    }

@greedy
rule CreateReadsDataPropertyForMappedTransferObjectTranferFieldDeclaration
    transform s : JSL!Expression
    to t: JUDOPSM!DataProperty {
        guard: s.isExpressionForTransferFieldReads() and s.eContainer.referenceType.isKindOf(JSL!PrimitiveDeclaration)
           and s.eContainer.eContainer.map.isDefined()

        t.setId("(jsl/" + s.getId() + ")/CreateReadsDataPropertyForMappedTransferObjectTransferFieldDeclaration");
        t.name = defaultReadsNamePrefix + s.eContainer.name + defaultReadsNameMidfix + s.eContainer.eContainer.name + defaultReadsNamePostfix;
        t.dataType = s.eContainer.referenceType.getPSMEquivalent();
        t.required = false;

		s.eContainer.eContainer.map.entity.getPSMEquivalent().dataProperties.add(t);

        log.debug("Created DataProperty for TransferField Reads: " + s);
    }
