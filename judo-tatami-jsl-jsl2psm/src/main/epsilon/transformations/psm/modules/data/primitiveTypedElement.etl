import "../../../../operations/_importAll.eol";
import "../namespace/namespace.etl";

@abstract
rule CreatePrimitiveTypedElement
    transform s: JSL!EntityMemberDeclaration
    to t: JUDOPSM!PrimitiveTypedElement {
        t.required = s.isRequired;
        t.name = s.name;
    }

rule CreateAttributeFromField
    transform s: JSL!EntityFieldDeclaration
    to t: JUDOPSM!Attribute
    extends CreatePrimitiveTypedElement {
        guard: s.referenceType.isKindOf(JSL!PrimitiveDeclaration)
        t.setId("(jsl/" + s.getId() + ")/CreateAttributeFromField");
        t.dataType = s.referenceType.getPSMEquivalent();
        s.eContainer.getPSMEquivalent().attributes.add(t);
        log.debug("Created Attribute (Field): " + t.name);
    }

rule CreateStaticDataDefaultValueForBinaryOperation
    transform s : JSL!BinaryOperation
    to t: JUDOPSM!StaticData {
        guard: s.isStaticDataDefaultValue()

        t.setId("(jsl/" + s.getId() + ")/CreateStaticDataDefaultValueForBinaryOperation");
        t.name = "_" + s.eContainer.name + "_default_" + s.eContainer.eContainer.name;
        t.dataType = s.eContainer.referenceType.getPSMEquivalent();
        t.required = false;

        var det = new JUDOPSM!DataExpressionType();
        det.expression = expressionUtils.getJql(s);

        t.getterExpression = det;

        s.eContainer.equivalent("CreateTransferAttributeForDefaultTransferObjectType").defaultValue = t;
        s.eContainer.eContainer.eContainer.getPSMEquivalent().elements.add(t);

        log.debug("Created StaticData for Default Value: " + det.expression);
    }

rule CreateDataPropertyDefaultValueForBinaryOperation
    transform s : JSL!BinaryOperation
    to t: JUDOPSM!DataProperty {
        guard: s.isDataPropertyDefaultValue()

        t.setId("(jsl/" + s.getId() + ")/CreateDataPropertyDefaultValueForBinaryOperation");
        t.name = "_" + s.eContainer.name + "_default_" + s.eContainer.eContainer.name;
        t.dataType = s.eContainer.referenceType.getPSMEquivalent();
        t.required = false;

        var det = new JUDOPSM!DataExpressionType();
        det.expression = expressionUtils.getJql(s);

        t.getterExpression = det;

        s.eContainer.equivalent("CreateTransferAttributeForDefaultTransferObjectType").defaultValue = t;
        s.eContainer.eContainer.getPSMEquivalent().dataProperties.add(t);

        log.debug("Created DataProperty for Default Value: " + det.expression);
    }

rule CreateAttributeFromIdentifier
    transform s: JSL!EntityIdentifierDeclaration
    to t: JUDOPSM!Attribute
    extends CreatePrimitiveTypedElement {
        t.setId("(jsl/" + s.getId() + ")/CreateAttributeFromIdentifier");

        t.dataType = s.referenceType.getPSMEquivalent();
        t.identifier = true;
        s.eContainer.getPSMEquivalent().attributes.add(t);
        log.debug("Created Attribute (Identifier): " + t.name);
    }
