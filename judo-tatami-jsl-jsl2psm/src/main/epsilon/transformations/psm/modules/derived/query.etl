
rule CreateUnmappedTransferObjectForQuery
    transform s : JSL!EntityQueryDeclaration
    to t : JUDOPSM!UnmappedTransferObjectType {
		guard: s.parameters.size() > 0
		
        t.setId("(jsl/" + s.getId() + ")/CreateUnmappedTransferObjectForQuery");
        t.name = defaultParameterNamePrefix + s.eContainer.name + defaultParameterNameMidfix + s.name + defaultParameterNamePostfix;
		s.eContainer.eContainer.getModelRoot().elements.add(t);
		log.debug("Created UnmappedTransferObjectType: " + t.name);
}

rule CreateTransferObjectForQueryDeclarationParameter
    transform s : JSL!QueryDeclarationParameter
    to t: JUDOPSM!TransferAttribute {
        guard: s.eContainer.isKindOf(JSL!EntityQueryDeclaration)
        
        t.setId("(jsl/" + s.getId() + ")/CreateTransferObjectForQueryDeclarationParameter");

        t.name = s.name;
        t.dataType = s.referenceType.getPSMEquivalent();
        
		s.eContainer.equivalent("CreateUnmappedTransferObjectForQuery").attributes.add(t);
        log.debug("Created Transfer Attribute for Entity Query Declaration: " + s.name);
}

rule CreateDataPropertyForQuery
    transform s : JSL!EntityQueryDeclaration
    to t : JUDOPSM!DataProperty
    {
      guard: s.referenceType.isKindOf(JSL!PrimitiveDeclaration)
      t.name = s.name;
      t.setId("(jsl/" + s.getId() + ")/CreateDataPropertyForQuery");
      t.getterExpression = s.equivalent("CreateGetterExpressionForParametrizedDataType");
      t.dataType = s.referenceType.getPSMEquivalent();

      s.eContainer.getPSMEquivalent().dataProperties.add(t);
      log.debug("Created Query Data Property: " + t.name);
}

rule CreateNavigationPropertyForQuery
    transform s : JSL!EntityQueryDeclaration
    to t : JUDOPSM!NavigationProperty
    {
      guard: s.referenceType.isKindOf(JSL!EntityDeclaration)
      t.name = s.name;
      t.setId("(jsl/" + s.getId() + ")/NavigationProperty");
      t.getterExpression = s.equivalent("CreateGetterExpressionForParametrizedReferenceType");
	  t.cardinality = s.equivalent("CreateCardinalityForEntityQueryDeclaration");
      t.target = s.referenceType.getPSMEquivalent();

      s.eContainer.getPSMEquivalent().navigationProperties.add(t);
      log.debug("Created Query Navigation Property: " + t.name);
}

@lazy
rule CreateGetterExpressionForParametrizedDataType
    transform s : JSL!EntityQueryDeclaration
    to t: JUDOPSM!DataExpressionType {
        t.setId("(jsl/" + s.getId() + ")/CreateGetterExpressionForParametrizedDataType");
        t.expression = expressionUtils.getJqlForEntityQuery(s, entityNamePrefix, entityNamePostfix);

		if (s.parameters.size() > 0) {
			t.parameterType = s.equivalent("CreateUnmappedTransferObjectForQuery");
		}

        log.debug("Created Data Expression Type for Parametrized Data Property: " + s.name);
}

@lazy
rule CreateGetterExpressionForParametrizedReferenceType
    transform s : JSL!EntityQueryDeclaration
    to t: JUDOPSM!ReferenceExpressionType {
        t.setId("(jsl/" + s.getId() + ")/CreateGetterExpressionForParametrizedReferenceType");
        t.expression = expressionUtils.getJqlForEntityQuery(s, entityNamePrefix, entityNamePostfix);

		if (s.parameters.size() > 0) {
			t.parameterType = s.equivalent("CreateUnmappedTransferObjectForQuery");
		}

        log.debug("Created Reference Expression Type for Parametrized Reference Type: " + s.name);
}

