@cached
operation JSL!TransferFieldDeclaration getId(): String {
    return (self.eContainer.getId() + "/" + self.name);
}

@cached
operation JSL!TransferFieldDeclaration isCalculated(): Boolean {
    return jslUtils.isCalculated(self);
}

@cached
operation JSL!TransferFieldDeclaration maps(): Boolean {
    return jslUtils.isMaps(self);
}

@cached
operation JSL!TransferFieldDeclaration reads(): Boolean {
    return jslUtils.isReads(self);
}

@cached
operation JSL!TransferFieldDeclaration getFqName(): String {
    return self.eContainer.getFqName() + "#" + self.name;
}

@cached
operation JSL!TransferFieldDeclaration isGetUploadTokenSupported(): Boolean {
	if (self.reads() and not self.maps()) {
		return false;
	}

	if (self.referenceType.primitive.isUndefined() or self.referenceType.primitive <> "binary") {
		return false;
	}

	if (self.eContainer.isCreateSupported()) {
		return true;
	}
	if (self.eContainer.map.isUndefined()) {
		return self.eContainer.isDefinedAsInputParameter();	
	}
	return false;
}


@cached 
operation JSL!TransferFieldDeclaration getTransferFieldDeclarationEquivalent() : JUDOPSM!TransferAttribute {
	if (not self.maps() and not self.reads()) {
		return self.equivalent("CreateTransientTransferAttribute");
	}
	if (self.reads()) {
		return self.equivalent("CreateDerivedTransferAttribute");
	}
	if (self.maps()) {
		return self.equivalent("CreateMappedTransferAttribute");
	}
	throw "Could not determinate attribute type: " + s.fqName();
}

@cached 
operation JSL!TransferFieldDeclaration isSortable() : Boolean {
	if (self.referenceType.`primitive`.isUndefined()) {
		return false;
	}
    return (self.maps() or self.reads()) and (
               self.referenceType.`primitive` == "string" or
               self.referenceType.`primitive` == "numeric"  or
               self.referenceType.`primitive` == "date" or
               self.referenceType.`primitive` == "timestamp" or
               self.referenceType.`primitive` == "time" or
               self.referenceType.`primitive` == "boolean"
           );
}

@cached 
operation JSL!TransferFieldDeclaration isFilterable() : Boolean {
	if (self.referenceType.`primitive`.isUndefined()) {
		return false;
	}
    return (self.maps() or self.reads()) and (
               self.referenceType.`primitive` == "string" or
               self.referenceType.`primitive` == "numeric"  or
               self.referenceType.`primitive` == "date" or
               self.referenceType.`primitive` == "timestamp" or
               self.referenceType.`primitive` == "time" or
               self.referenceType.`primitive` == "boolean"
           );
}

