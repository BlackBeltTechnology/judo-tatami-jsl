rule AccessViewPageDefinition
    transform s: JSL!UIMenuLinkDeclaration
    to t: UI!ui::PageDefinition {
        guard: rootMenu.containsVisualElement(s)

        var relation = s.actorAccess.target;

        t.setId(rootMenu.name + "/(jsl/" + s.getId() + ")/AccessViewPageDefinition");
        t.name = s.getFqName() + "::AccessViewPage";
        t.container = s.referenceType.equivalent("ViewPageContainer");

        rootMenu.equivalent("Application").pages.add(t);

        t.dataElement = relation.equivalent("RelationType");

        /*
        log.info("===================");
        log.info(t.name + ":");
        log.info(relations.collect(r | r.name).concat(", "));
        log.info("===================");
        */

        for (link in s.referenceType.getOwnLinks()) {
            var lRelation = link.transferRelation.target;

            t.actions.add(link.equivalentDiscriminated("ViewLinkDeclarationOpenPageAction", s.getId()));
            if (lRelation.isRefreshAllowed() and not lRelation.isEager()) {
                t.actions.add(link.equivalentDiscriminated("ViewLinkDeclarationRefreshAction", s.getId()));
            }
            if (link.getCreateFormModifier().isDefined()) {
                t.actions.add(link.equivalentDiscriminated("ViewLinkDeclarationOpenFormAction", s.getId()));
            }
            if (lRelation.isDeleteAllowed()) {
                t.actions.add(link.equivalentDiscriminated("ViewLinkDeclarationRowDeleteAction", s.getId()));
            }
            if (link.getSelectorTableModifier().isDefined()) {
                t.actions.add(link.equivalentDiscriminated("ViewLinkDeclarationOpenSetSelectorDialogAction", s.getId()));
            }
            if (link.getSelectorTableModifier().isDefined()) {
                t.actions.add(link.equivalentDiscriminated("ViewLinkDeclarationUnsetAction", s.getId()));
            }
        }

        for (table in s.referenceType.getOwnTables()) {
            var tRelation = table.transferRelation.target;

            if (table.getUpdateViewModifier().isDefined()) {
                t.actions.add(table.equivalentDiscriminated("ViewTableDeclarationOpenPageAction", s.getId()));
            }
            if (tRelation.isFilterSupported()) {
                t.actions.add(table.equivalentDiscriminated("ViewTableDeclarationFilterAction", s.getId()));
            }
            if (tRelation.isRefreshAllowed()) {
                t.actions.add(table.equivalentDiscriminated("ViewTableDeclarationRefreshAction", s.getId()));
            }
            if (table.getCreateFormModifier().isDefined()) {
                t.actions.add(table.equivalentDiscriminated("ViewTableDeclarationOpenCreateAction", s.getId()));
            }
            if (tRelation.isDeleteAllowed()) {
                t.actions.add(table.equivalentDiscriminated("ViewTableDeclarationRowDeleteAction", s.getId()));
            }
            if (table.getSelectorTableModifier().isDefined()) {
                t.actions.add(table.equivalentDiscriminated("ViewTableDeclarationOpenAddSelectorAction", s.getId()));
            }
            if (table.getSelectorTableModifier().isDefined()) {
                t.actions.add(table.equivalentDiscriminated("ViewTableDeclarationClearAction", s.getId()));
                t.actions.add(table.equivalentDiscriminated("ViewTableDeclarationBulkRemoveAction", s.getId()));
            }
        }

        t.actions.add(s.equivalent("AccessViewBackAction"));
        if (relation.isRefreshAllowed()) {
            t.actions.add(s.equivalent("AccessViewRefreshAction"));
        }
        if (relation.isUpdateAllowed()) {
            t.actions.add(s.equivalent("AccessViewUpdateAction"));
        }
        if (relation.isDeleteAllowed()) {
            t.actions.add(s.equivalent("AccessViewDeleteAction"));
        }

        log.debug("Create AccessViewPageDefinition: " + t.name);
}

@lazy
rule AccessViewBackAction
    transform s: JSL!UIMenuLinkDeclaration
    to t: UI!ui::Action {
        t.setId(rootMenu.name + "/(jsl/" + s.getId() + ")/AccessViewBackAction");
        t.name = s.name + "::Back";
        t.actionDefinition = s.referenceType.equivalent("ViewPageContainerBackActionDefinition");

        log.debug("AccessViewBackAction: " + t.name);
}

@lazy
rule AccessViewRefreshAction
    transform s: JSL!UIMenuLinkDeclaration
    to t: UI!ui::Action {
	    t.setId(rootMenu.name + "/(jsl/" + s.getId() + ")/AccessViewRefreshAction");
 		t.name = s.getFqName() + "::Refresh";
        t.ownerDataElement = s.actorAccess.target.equivalent("RelationType");
        t.actionDefinition = s.referenceType.equivalent("ViewPageContainerRefreshActionDefinition");

        log.debug("AccessViewRefreshAction: " + t.name);
}

@lazy
rule AccessViewUpdateAction
    transform s: JSL!UIMenuLinkDeclaration
    to t: UI!ui::Action {
	    t.setId(rootMenu.name + "/(jsl/" + s.getId() + ")/AccessViewUpdateAction");
 		t.name = s.getFqName() + "::Update";
        t.ownerDataElement = s.actorAccess.target.equivalent("RelationType");
        t.actionDefinition = s.referenceType.equivalent("ViewPageContainerUpdateActionDefinition");

        log.debug("AccessViewUpdateAction: " + t.name);
}

@lazy
rule AccessViewDeleteAction
    transform s: JSL!UIMenuLinkDeclaration
    to t: UI!ui::Action {
	    t.setId(rootMenu.name + "/(jsl/" + s.getId() + ")/AccessViewDeleteAction");
 		t.name = s.getFqName() + "::Delete";
        t.ownerDataElement = s.actorAccess.target.equivalent("RelationType");
        t.actionDefinition = s.referenceType.equivalent("ViewPageContainerDeleteActionDefinition");

        log.debug("AccessViewDeleteAction: " + t.name);
}
