rule RowLinkPageDefinition
    transform s: JSL!RowLinkDeclaration
    to t: UI!ui::PageDefinition {
        guard: actorDeclaration.getAllRelations().contains(s)

        t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/RowLinkPageDefinition");

        t.name = s.getFqName() + "::View::PageDefinition";
        t.container = s.referenceType.equivalent("TransferDeclarationPageContainer");

        actorDeclaration.equivalent("Application").pages.add(t);

        t.dataElement = s.equivalent("RelationType");

        var relations = s.referenceType.getDirectRelations();

        /*
        log.info("===================");
        log.info(t.name + ":");
        log.info(relations.collect(r | r.name).concat(", "));
        log.info("===================");
        */

        for (link in relations.select(r | r.isKindOf(JSL!ViewLinkDeclaration))) {
            t.actions.add(link.equivalentDiscriminated("ViewLinkDeclarationOpenPageAction", "RowLinkPageDefinition"));

            if (link.isRefreshAllowed() and not link.isEager()) {
                t.actions.add(link.equivalentDiscriminated("ViewLinkDeclarationRefreshAction", "RowLinkPageDefinition"));
            }
            if (link.isCreateAllowed()) {
                t.actions.add(link.equivalentDiscriminated("ViewLinkDeclarationOpenFormAction", "RowLinkPageDefinition"));
            }
            if (link.isDeleteAllowed()) {
                t.actions.add(link.equivalentDiscriminated("ViewLinkDeclarationRowDeleteAction", "RowLinkPageDefinition"));
            }
        }

        for (table in relations.select(r | r.isKindOf(JSL!ViewTableDeclaration))) {
            var detailLink = table.getDetailLink();

            if (detailLink.isDefined()) {
                t.actions.add(table.equivalentDiscriminated("ViewTableDeclarationOpenPageAction", "RowLinkPageDefinition"));
            }
            if (table.isFilterSupported()) {
                t.actions.add(table.equivalentDiscriminated("ViewTableDeclarationFilterAction", "RowLinkPageDefinition"));
            }
            if (table.isRefreshAllowed()) {
                t.actions.add(table.equivalentDiscriminated("ViewTableDeclarationRefreshAction", "RowLinkPageDefinition"));
            }
            if (table.isDeleteAllowed()) {
                t.actions.add(table.equivalentDiscriminated("ViewTableDeclarationRowDeleteAction", "RowLinkPageDefinition"));
            }
        }

        t.actions.add(s.equivalent("RowLinkPageDefinitionBackAction"));
        if (s.isRefreshAllowed()) {
            t.actions.add(s.equivalent("RowLinkPageDefinitionRefreshAction"));
        }
        if (s.isUpdateAllowed()) {
            t.actions.add(s.equivalent("RowLinkPageDefinitionUpdateAction"));
        }
        if (s.isDeleteAllowed()) {
            t.actions.add(s.equivalent("RowLinkPageDefinitionDeleteAction"));
        }

        log.debug("Create RowLinkPageDefinition: " + t.name);
}

@lazy
@greedy
rule RowLinkPageDefinitionRefreshAction
    transform s: JSL!RowLinkDeclaration
    to t: UI!ui::Action {
        guard: actorDeclaration.getAllRelations().contains(s)

        t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/RowLinkPageDefinitionRefreshAction");
 		t.name = s.name + "::Refresh";
        t.actionDefinition = s.referenceType.equivalent("TransferDeclarationRefreshActionDefinition");

        log.debug("RowLinkPageDefinitionRefreshAction: " + t.name);
}

@lazy
@greedy
rule RowLinkPageDefinitionBackAction
    transform s: JSL!RowLinkDeclaration
    to t: UI!ui::Action {
        guard: actorDeclaration.getAllRelations().contains(s)

        t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/RowLinkPageDefinitionBackAction");
 		t.name = s.name + "::Back";
        t.actionDefinition = s.referenceType.equivalent("TransferDeclarationBackActionDefinition");

        log.debug("RowLinkPageDefinitionBackAction: " + t.name);
}

@lazy
@greedy
rule RowLinkPageDefinitionUpdateAction
    transform s: JSL!RowLinkDeclaration
    to t: UI!ui::Action {
        t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/RowLinkPageDefinitionUpdateAction");

 		t.name = s.name + "::Update";
        t.actionDefinition = s.referenceType.equivalent("TransferDeclarationUpdateActionDefinition");

        log.debug("RowLinkPageDefinitionUpdateAction: " + t.name);
}

@lazy
@greedy
rule RowLinkPageDefinitionDeleteAction
    transform s: JSL!RowLinkDeclaration
    to t: UI!ui::Action {
        t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/RowLinkPageDefinitionDeleteAction");

 		t.name = s.name + "::Delete";
        t.actionDefinition = s.referenceType.equivalent("TransferDeclarationDeleteActionDefinition");

        log.debug("RowLinkPageDefinitionDeleteAction: " + t.name);
}
