rule ViewLinkPageDefinition
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::PageDefinition {
        guard: actorDeclaration.getAllRelations().contains(s)

        t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkPageDefinition");

        t.name = s.getFqName() + "::View::PageDefinition";
        t.container = s.referenceType.equivalent("TransferDeclarationPageContainer");

        actorDeclaration.equivalent("Application").pages.add(t);

        t.dataElement = s.equivalent("RelationType");

        var relations = s.referenceType.getDirectRelations();

        /*
        log.info("===================");
        log.info(t.name + ":");
        log.info(relations.collect(r | r.name).concat(", "));
        log.info("===================");
        */

        for (link in relations.select(r | r.isKindOf(JSL!ViewLinkDeclaration))) {
            t.actions.add(link.equivalent("ViewLinkDeclarationOpenPageAction"));

            if (link.isCreateAllowed()) {
                t.actions.add(link.equivalent("ViewLinkDeclarationOpenFormAction"));
            }
            if (link.isDeleteAllowed()) {
                t.actions.add(link.equivalent("ViewLinkDeclarationRowDeleteAction"));
            }
        }

        for (table in relations.select(r | r.isKindOf(JSL!ViewTableDeclaration))) {
            var detailLink = table.getDetailLink();

            if (detailLink.isDefined()) {
                t.actions.add(table.equivalent("ViewTableDeclarationOpenPageAction"));
            }
            if (table.isFilterSupported()) {
                t.actions.add(table.equivalent("ViewTableDeclarationFilterAction"));
            }
            if (table.isRefreshAllowed()) {
                t.actions.add(table.equivalent("ViewTableDeclarationRefreshAction"));
            }
            if (table.isDeleteAllowed()) {
                t.actions.add(table.equivalent("ViewTableDeclarationRowDeleteAction"));
            }
        }

        t.actions.add(s.equivalent("ViewLinkPageDefinitionBackAction"));
        if (s.isUpdateAllowed()) {
            t.actions.add(s.equivalent("ViewLinkPageDefinitionUpdateAction"));
        }
        if (s.isDeleteAllowed()) {
            t.actions.add(s.equivalent("ViewLinkPageDefinitionDeleteAction"));
        }

        log.debug("Create ViewLinkPageDefinition: " + t.name);
}

@lazy
@greedy
rule ViewLinkPageDefinitionBackAction
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::Action {
        guard: actorDeclaration.getAllRelations().contains(s)

        t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkPageDefinitionBackAction");
 		t.name = s.name + "::Back";
        t.actionDefinition = s.referenceType.equivalent("TransferDeclarationBackActionDefinition");

        log.debug("ViewLinkPageDefinitionBackAction: " + t.name);
}

@lazy
@greedy
rule ViewLinkPageDefinitionUpdateAction
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::Action {
        t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkPageDefinitionUpdateAction");

 		t.name = s.name + "::Update";
        t.actionDefinition = s.referenceType.equivalent("TransferDeclarationUpdateActionDefinition");

        log.debug("ViewLinkPageDefinitionUpdateAction: " + t.name);
}

@lazy
@greedy
rule ViewLinkPageDefinitionDeleteAction
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::Action {
        t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkPageDefinitionDeleteAction");

 		t.name = s.name + "::Delete";
        t.actionDefinition = s.referenceType.equivalent("TransferDeclarationDeleteActionDefinition");

        log.debug("ViewLinkPageDefinitionDeleteAction: " + t.name);
}

@greedy
rule ViewLinkCreateFormPageDefinition
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::PageDefinition {
        guard: actorDeclaration.getAllRelations().contains(s)
            and s.referenceType.getCreateEventDeclaration().isDefined()
            and s.referenceType.getCreateEventDeclaration().parameterType.isDefined()

        t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkCreateFormPageDefinition");

        t.name = s.getFqName() + "::Create::PageDefinition";

        var targetCreateEvent = s.referenceType.getCreateEventDeclaration();
        var targetContainer = targetCreateEvent.parameterType.isDefined() ? targetCreateEvent.parameterType : s.referenceType;
        t.container = targetContainer.equivalent("TransferDeclarationPageContainer");

        actorDeclaration.equivalent("Application").pages.add(t);

        t.dataElement = s.equivalent("RelationType");

        // var relations = s.referenceType.getDirectRelations();

        /*
        log.info("===================");
        log.info(t.name + ":");
        log.info(relations.collect(r | r.name).concat(", "));
        log.info("===================");
        */
        /*
        for (link in relations.select(r | r.isKindOf(JSL!ViewLinkDeclaration))) {
            t.actions.add(link.equivalent("ViewLinkDeclarationOpenPageAction"));
        }

        for (table in relations.select(r | r.isKindOf(JSL!ViewTableDeclaration))) {
            var detailLink = table.getDetailLink();

            if (detailLink.isDefined()) {
                t.actions.add(table.equivalent("ViewTableDeclarationOpenPageAction"));
            }
        }
        */

        t.actions.add(s.equivalent("ViewLinkCreateFormBackAction"));
        t.actions.add(s.equivalent("ViewLinkCreateFormCreateAction"));

        log.debug("Create ViewLinkCreateFormPageDefinition: " + t.name);
}

@greedy
@lazy
rule ViewLinkCreateFormCreateAction
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::Action {
        t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkCreateFormCreateAction");

        var targetCreateEvent = s.referenceType.getCreateEventDeclaration();
        var targetContainer = targetCreateEvent.parameterType.isDefined() ? targetCreateEvent.parameterType : s.referenceType;

 		t.name = s.name + "::Create";
        t.actionDefinition = targetContainer.equivalent("TransferDeclarationCreateActionDefinition");

        log.debug("ViewLinkCreateFormCreateAction: " + t.name);
}

@greedy
@lazy
rule ViewLinkCreateFormBackAction
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::Action {
        t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkCreateFormBackAction");

        var targetCreateEvent = s.referenceType.getCreateEventDeclaration();
        var targetContainer = targetCreateEvent.parameterType.isDefined() ? targetCreateEvent.parameterType : s.referenceType;

 		t.name = s.name + "::Back";
        t.actionDefinition = targetContainer.equivalent("TransferDeclarationBackActionDefinition");

        log.debug("ViewLinkCreateFormBackAction: " + t.name);
}

@abstract
rule AbstractViewLinkDeclaration
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::Link {
        guard: actorDeclaration.getAllRelations().contains(s)

    	t.name = s.name;
    	t.relationName = s.name;
    	if (s.getLabelModifier().isDefined()) {
            t.label = s.getLabelModifier().value.value;
        }
        if (s.getIconModifier().isDefined()) {
            t.icon = s.equivalent("LinkIcon");
        }
        t.row = 1d;
        t.col = s.width.isDefined() ? s.width.asReal() : 12d;
        t.~pos = s.~pos;
        t.isEager = s.isEager();
        if (t.~pos.isUndefined()) {
            t.~pos = 0;
        }

        if (s.isEager()) {
            // TODO finish additional mas attributes
            /*
            for (dataFeature in s.additionalMaskFeatures) {
                var attributeType = dataFeature.getMember().mapAttributeType(s.getRoot().getTransferObjectType().equivalent("ClassType"));
                t.additionalMaskAttributes.add(attributeType);
            }
            */
        }
}

@lazy
@greedy
rule LinkIcon
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::Icon {
    	t.iconName = s.getIconModifier().value.value;
    	t.name = s.name + "ViewLinkIcon";
    	t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/LinkIcon");
}

rule InlineViewLink
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::Link
    extends AbstractViewLinkDeclaration {
        guard: s.getTransferObjectType().isGenerated()

        var id = actorDeclaration.name + "/(jsl/" + s.getId() + ")/InlineViewLink";
        t.setId(id);
        t.dataElement = s.equivalent("RelationType");

        s.eContainer.uiContainer().children.add(t);

        var col = s.equivalentDiscriminated("ViewLinkDeclarationRepresentationColumn", id);
        t.parts.add(col);

        // TODO transform selector row per page
        t.selectorRowsPerPage = 10;
        t.autoCompleteRows = 10;

        t.actionButtonGroup = s.equivalent("InlineViewLinkButtonGroup");

        /*
        t.autocompleteRangeActionDefinition = s.equivalent("TabularReferenceFieldLinkAutocompleteRangeActionDefinition");
        t.autocompleteSetActionDefinition = s.equivalent("TabularReferenceFieldLinkAutocompleteSetActionDefinition");

        if (relation.isRefreshSupported() and relation.relationKind == ESM!esm::structure::RelationKind#ASSOCIATION) {
            t.refreshActionDefinition = s.equivalent("TabularReferenceFieldLinkRefreshActionDefinition");
        }
        */

        log.debug("InlineViewLink: " + t.name);
}

@lazy
rule InlineViewLinkButtonGroup
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::ButtonGroup {
        t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/InlineViewLinkButtonGroup");
        t.name = s.name + "::Actions";
        t.label = "Actions";

        // TODO add action buttons

        t.buttons.add(s.equivalent("ViewLinkDeclarationOpenPageButton"));

        if (s.isCreateAllowed()) {
            t.buttons.add(s.equivalent("ViewLinkDeclarationOpenFormButton"));
        }
        if (s.isDeleteAllowed()) {
            t.buttons.add(s.equivalent("ViewLinkDeclarationDeleteButton"));
        }

        log.debug("InlineViewLinkButtonGroup: " + t.name);
}

@lazy
rule ViewLinkDeclarationRepresentationColumn
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::Column {
        t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkDeclarationRepresentationColumn");

        var firstStringFromTarget = s.referenceType.getAllPrimitiveFields().selectOne(f | f.referenceType.isDefined() and f.referenceType.`primitive` == "string");

        if (firstStringFromTarget.isUndefined()) {
            throw "Could not get string type field from relation target to use for representation: " + s.referenceType.name;
        }

        t.attributeType = firstStringFromTarget.getTransferFieldDeclarationEquivalent();
        t.name = t.attributeType.name;
        t.label = "";
        t.col = 12d;
        t.row = 1d;
        t.format = "%s";
        t.sort = UI!ui::Sort#ASC;

        log.debug("ViewLinkDeclarationRepresentationColumn: " + t.name);
}

@lazy
rule ViewLinkDeclarationOpenPageButton
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::Button {
	    t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkDeclarationOpenPageButton");
 		t.name = s.name + "::View";
    	t.buttonStyle = "contained";
    	t.label = "View";
    	t.icon = s.equivalent("ViewLinkDeclarationOpenPageButtonIcon");
    	t.actionDefinition = s.equivalent("ViewLinkDeclarationOpenPageActionDefinition");

        log.debug("ViewLinkDeclarationOpenPageButton: " + t.name);
}

@lazy
rule ViewLinkDeclarationOpenPageButtonIcon
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::Icon {
    	t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkDeclarationOpenPageButtonIcon");
    	t.iconName = "eye";
    	t.name = s.name + "OpenPageIcon";

    	log.debug("ViewLinkDeclarationOpenPageButtonIcon: " + t.name);
}

@lazy
rule ViewLinkDeclarationOpenPageActionDefinition
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::OpenPageActionDefinition {
	    t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkDeclarationOpenPageActionDefinition");
 		t.name = s.name + "::View";
 		t.isContainedRelationAction = true;
 		t.targetType = s.referenceType.equivalent("ClassType");

        log.debug("ViewLinkDeclarationOpenPageActionDefinition: " + t.name);
}

@greedy
@lazy
rule ViewLinkDeclarationOpenPageAction
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::Action {
        t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkDeclarationOpenPageAction");

 		t.name = s.name + "::OpenPage";
        t.actionDefinition = s.equivalent("ViewLinkDeclarationOpenPageActionDefinition");

        t.targetPageDefinition = s.equivalent("ViewLinkPageDefinition");

        log.debug("ViewLinkDeclarationOpenPageAction: " + t.name);
}


@lazy
rule ViewLinkDeclarationOpenFormButton
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::Button {
	    t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkDeclarationOpenFormButton");
 		t.name = s.name + "::Create::Open";
    	t.buttonStyle = "contained";
    	t.label = "Create";
    	t.icon = s.equivalent("ViewLinkDeclarationOpenFormButtonIcon");
    	t.actionDefinition = s.equivalent("ViewLinkDeclarationOpenCreateFormActionDefinition");

        log.debug("ViewLinkDeclarationOpenFormButton: " + t.name);
}

@lazy
rule ViewLinkDeclarationOpenFormButtonIcon
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::Icon {
    	t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkDeclarationOpenFormButtonIcon");
    	t.iconName = "note-add";
    	t.name = s.name + "OpenFormIcon";

    	log.debug("ViewLinkDeclarationOpenFormButtonIcon: " + t.name);
}

@lazy
rule ViewLinkDeclarationOpenCreateFormActionDefinition
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::OpenCreateFormActionDefinition {
	    t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkDeclarationOpenCreateFormActionDefinition");
 		t.name = s.name + "::Create";
 		t.isContainedRelationAction = true;

        var targetCreateEvent = s.referenceType.getCreateEventDeclaration();

 		if (targetCreateEvent.parameterType.isDefined() and targetCreateEvent.parameterType.isKindOf(JSL!TransferDeclaration)) {
            t.targetType = targetCreateEvent.parameterType.equivalent("ClassType");
 		} else {
 		    t.targetType = s.referenceType.equivalent("ClassType");
 		}

        log.debug("ViewLinkDeclarationOpenCreateFormActionDefinition: " + t.name);
}


@greedy
@lazy
rule ViewLinkDeclarationOpenFormAction
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::Action {
        t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkDeclarationOpenFormAction");

 		t.name = s.name + "::OpenForm";
        t.actionDefinition = s.equivalent("ViewLinkDeclarationOpenCreateFormActionDefinition");

        t.targetPageDefinition = s.equivalent("ViewLinkCreateFormPageDefinition");

        log.debug("ViewLinkDeclarationOpenFormAction: " + t.name);
}

@lazy
rule ViewLinkDeclarationDeleteButton
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::Button {
	    t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkDeclarationDeleteButton");
 		t.name = s.name + "::Delete";
    	t.buttonStyle = "contained";
    	t.label = "Delete";
    	t.icon = s.equivalent("ViewLinkDeclarationDeleteButtonIcon");
    	t.actionDefinition = s.equivalent("ViewLinkDeclarationRowDeleteActionDefinition");

        log.debug("ViewLinkDeclarationDeleteButton: " + t.name);
}

@lazy
rule ViewLinkDeclarationDeleteButtonIcon
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::Icon {
    	t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkDeclarationDeleteButtonIcon");
    	t.iconName = "delete_forever";
    	t.name = s.name + "DeleteIcon";

    	log.debug("ViewLinkDeclarationDeleteButtonIcon: " + t.name);
}

@lazy
rule ViewLinkDeclarationRowDeleteActionDefinition
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::RowDeleteActionDefinition {
	    t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkDeclarationRowDeleteActionDefinition");
 		t.name = s.name + "::Delete";
 		t.isContainedRelationAction = true;
        t.targetType = s.referenceType.equivalent("ClassType");

        log.debug("ViewLinkDeclarationRowDeleteActionDefinition: " + t.name);
}

@greedy
@lazy
rule ViewLinkDeclarationRowDeleteAction
    transform s: JSL!ViewLinkDeclaration
    to t: UI!ui::Action {
        t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ViewLinkDeclarationRowDeleteAction");

 		t.name = s.name + "::RowDelete";
        t.actionDefinition = s.equivalent("ViewLinkDeclarationRowDeleteActionDefinition");

        log.debug("ViewLinkDeclarationRowDeleteAction: " + t.name);
}
