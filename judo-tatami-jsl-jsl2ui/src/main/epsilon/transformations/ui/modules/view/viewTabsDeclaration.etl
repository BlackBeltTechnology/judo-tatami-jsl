rule TabBarVisualElement
    transform s: JSL!UIViewTabsDeclaration
    to t: UI!ui::TabController {
        guard: rootMenu.containsVisualElement(s)

        t.setId(rootMenu.name + "/(jsl/" + s.getId() + ")/TabBarVisualElement");
        t.name = s.name;
        t.~pos = s.~pos;
        t.col = s.getWidth().isDefined() ? s.getWidth().value.asReal() : 12d;
        t.label = s.getLabelWithNameFallback();
        if (s.getIconModifier().isDefined()) {
            t.icon = s.equivalent("TabsIcon");
        }
        if (s.getOrientationModifier().isDefined()) {
            if (s.getOrientationModifier().isVertical()) {
                t.orientation = UI!ui::TabOrientation#VERTICAL;
            } else {
                t.orientation = UI!ui::TabOrientation#HORIZONTAL;
            }
        }

        s.eContainer.uiContainer().children.add(t);

        log.debug("TabBarVisualElement: " + t.name);
}

@greedy
rule SubTab
    transform s: JSL!UIViewTabsDeclaration
    to t: UI!ui::Tab {
        guard: s.eContainer.isKindOf(JSL!UIViewTabsDeclaration) and rootMenu.containsVisualElement(s)

        s.eContainer.equivalent("TabBarVisualElement").tabs.add(t);
        t.element = s.equivalent("TabBarVisualElement");
        t.name = t.element.name;
        t.setId(rootMenu.name + "/(jsl/" + s.getId() + ")/SubTab");

        log.debug("SubTab: " + t.name);
}

@lazy
@greedy
rule TabsIcon
    transform s: JSL!UIViewTabsDeclaration
    to t: UI!ui::Icon {
        t.iconName = s.getIconModifier().value.value;
        t.name = s.name + "TabsIcon";
        t.setId(rootMenu.name + "/(jsl/" + s.getId() + ")/TabsIcon");
}
