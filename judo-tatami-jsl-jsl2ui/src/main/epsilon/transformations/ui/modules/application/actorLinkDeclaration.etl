rule ActorNavigationItem
    transform s: JSL!ActorLinkDeclaration
    to t : UI!ui::NavigationItem {
        guard: s.getActorDeclaration().isDefined() and s.getActorDeclaration() == actorDeclaration

        t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/ActorNavigationItem");
        t.name = s.getFqName();

        var label = s.getLabelModifier();
        if (label.isDefined()) {
            t.label = label.value.value;
        }

        var icon = s.getIconModifier();
        if (icon.isDefined()) {
            t.icon = icon.equivalent("IconModifierIcon");
        }

        t.target = s.equivalent("AccessPageDefinition");

        if (s.eContainer.isTypeOf(JSL!ActorGroupDeclaration)) {
            s.eContainer.equivalent("MenuItemGroup").items.add(t);
            t.~pos = s.eContainer.members.indexOf(s);
        } else {
            var navigationController = actorDeclaration.equivalent("Application").navigationController;
            navigationController.items.add(t);
            t.~pos = s.eContainer.members.indexOf(s);
        }

        log.debug("ActorNavigationItem: " + s.name);
    }

rule AccessPageDefinition
    transform s: JSL!ActorLinkDeclaration
    to t: UI!ui::PageDefinition {
        guard: s.getActorDeclaration().isDefined() and s.getActorDeclaration() == actorDeclaration

        t.setId(actorDeclaration.name + "/(jsl/" + s.getId() + ")/AccessPageDefinition");

        t.name = s.getFqName() + "::View::PageDefinition";
        t.container = s.referenceType.equivalent("TransferDeclarationPageContainer");

        actorDeclaration.equivalent("Application").pages.add(t);

        t.dataElement = s.equivalent("RelationType");

        var relations = s.referenceType.getDirectRelations();

        /*
        log.info("===================");
        log.info(t.name + ":");
        log.info(relations.collect(r | r.name).concat(", "));
        log.info("===================");
        */

        for (link in relations.select(r | r.isKindOf(JSL!ViewLinkDeclaration))) {
            t.actions.add(link.equivalentDiscriminated("ViewLinkDeclarationOpenPageAction", "AccessPageDefinition"));
            if (link.isCreateAllowed()) {
                t.actions.add(link.equivalentDiscriminated("ViewLinkDeclarationOpenFormAction", "AccessPageDefinition"));
            }
            if (link.isDeleteAllowed()) {
                t.actions.add(link.equivalentDiscriminated("ViewLinkDeclarationRowDeleteAction", "AccessPageDefinition"));
            }
        }

        for (table in relations.select(r | r.isKindOf(JSL!ViewTableDeclaration))) {
            var detailLink = table.getDetailLink();

            if (detailLink.isDefined()) {
                t.actions.add(table.equivalentDiscriminated("ViewTableDeclarationOpenPageAction", "AccessPageDefinition"));
            }
            if (table.isFilterSupported()) {
                t.actions.add(table.equivalentDiscriminated("ViewTableDeclarationFilterAction", "AccessPageDefinition"));
            }
            if (table.isRefreshAllowed()) {
                t.actions.add(table.equivalentDiscriminated("ViewTableDeclarationRefreshAction", "AccessPageDefinition"));
            }
            if (table.isDeleteAllowed()) {
                t.actions.add(table.equivalentDiscriminated("ViewTableDeclarationRowDeleteAction", "AccessPageDefinition"));
            }
        }

        t.actions.add(s.equivalent("ViewLinkPageDefinitionBackAction"));
        if (s.isUpdateAllowed()) {
            t.actions.add(s.equivalent("ViewLinkPageDefinitionUpdateAction"));
        }
        if (s.isDeleteAllowed()) {
            t.actions.add(s.equivalent("ViewLinkPageDefinitionDeleteAction"));
        }

        log.debug("Create AccessPageDefinition: " + t.name);
}
