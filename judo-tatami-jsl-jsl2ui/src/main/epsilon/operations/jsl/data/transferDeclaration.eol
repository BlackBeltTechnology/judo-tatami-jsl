@cached
operation JSL!TransferDeclaration getId(): String {
    return (self.eContainer.getId() + "/" + self.name);
}

@cached
operation JSL!TransferDeclaration getFqName(): String {
    return self.eContainer.getFqName() + "::" + self.name;
}

@cached
operation JSL!TransferDeclaration getActorDeclaration(): JSL!ActorDeclaration {
    if (self.eContainer.isTypeOf(JSL!ActorDeclaration)) {
        return self.eContainer;
    } else if (self.eContainer.isTypeOf(JSL!ModelDeclaration)) {
        return self.eContainer.declarations.selectOne(e | e.isTypeOf(JSL!ActorDeclaration));
    }
    return null;
}

@cached
operation JSL!TransferDeclaration getExposedTransferObjects(): Set {
    var transfers = new Set();
    var relations = self.getExposedRelations();

    transfers.addAll(relations.collect(r | r.referenceType).asSet());

    for (relation in relations) {
        var detail = relation.getDetail();

        if (detail.isDefined() and detail.view.isDefined()) {
            transfers.add(detail.view);
        }
    }

    return transfers;
}

@cached
operation JSL!TransferDeclaration getExposedRelations(): Set {
    return self.collectExposedRelations(new Set());
}

operation JSL!TransferDeclaration collectExposedRelations(collected: Set): Set {
    var relations = self.getAllRelations();
    // var operations = self.getAllOperations().select(op | not op.hasQueryParameters());
    /*relations.addAll(operations
        .select(o | o.input.isDefined())
        .collect(o | o.input.target)
        .collect(i | i.getAllRelations().select(r | not r.isQuery)).flatten());
    relations.addAll(operations
        .select(o | o.output.isDefined())
        .collect(o | o.output.target)
        .collect(o | o.getAllRelations().select(r | not r.isQuery)).flatten());
    */
    for (relation in relations) {
        if ((not collected.includes(relation)) and relation.referenceType.isKindOf(JSL!TransferDeclaration)) {
            collected.add(relation);
            relation.referenceType.collectExposedRelations(collected);
        }
    }

    return collected;
}

@cached
operation JSL!TransferDeclaration getAllRelations(): Set {
    var relations = new Set();
    relations.addAll(self.members.select(m | m.isKindOf(JSL!TransferRelationDeclaration)));

    if (self.isKindOf(JSL!ActorDeclaration)) {
        var linkRelations = self.getAllMenuDeclarations().asSet();
        relations.addAll(linkRelations);
    }

    return relations;
}

@cached
operation JSL!TransferDeclaration getAllPrimitiveFields(): Set {
    return self.members.select(m | m.isKindOf(JSL!TransferFieldDeclaration) and m.`primitive`.isDefined());
}

@cached
operation JSL!TransferDeclaration isDeleteSupported(): Boolean {
	if (self.map.isDefined()) {
		return self.members.exists(m | m.isKindOf(JSL!TransferDeleteDeclaration) and m.isEventInstead());
	}
	return false;
}

@cached
operation JSL!TransferDeclaration isCreateSupported(): Boolean {
	if (self.map.isDefined()) {
		return self.members.exists(m | m.isKindOf(JSL!TransferCreateDeclaration) and m.isEventInstead());
	}
	return false;
}

@cached
operation JSL!TransferDeclaration isUpdateSupported(): Boolean {
	if (self.map.isDefined()) {
		return self.members.exists(m | m.isKindOf(JSL!TransferUpdateDeclaration) and m.isEventInstead());
	}
	return false;
}

@cached
operation JSL!TransferDeclaration isDefinedAsInputParameter(): Boolean {
	return JSL!TransferActionDeclaration.all().collect(a | a.getParameterType()).contains(self);
}

@cached
operation JSL!TransferDeclaration isGetTemplateSupported(): Boolean {
	if (self.isCreateSupported()) {
		return true;
	}
	if (self.map.isUndefined()) {
		return self.isDefinedAsInputParameter();
	}
	return false;
}

@cached
operation JSL!TransferDeclaration isRefreshSupported(): Boolean {
    return self.map.isDefined();
}
